10 REM Test program for integer BASICs
100 PRINT "*** Core language features ***"
110 A=5: B=19: C=A-B: D=A+B: E=A*B: F=INT(B/A)
120 PRINT "Math:"
130 PRINT A,B,C,D,E,F
140 PRINT A,B,A-B,A+B,A*B,INT(B/A)
150 PRINT "5 19 -14 24 95 3"
200 PRINT "Loop and GOTO:"
210 FOR I=1 TO 10
220 PRINT I,
230 NEXT: PRINT 
240 FOR I=1
250 PRINT I,: IF I=10 BREAK
260 NEXT: PRINT
270 I=1
280 PRINT I,: I=I+1: IF I<=10: GOTO 280
290 PRINT
300 PRINT "GOSUB and RETURN"
310 GOSUB 340
320 PRINT "Returned"
330 GOTO 400
340 PRINT "In GOSUB"
350 RETURN
400 PRINT "Functions"
410 A=-1: B=1: @R=1
420 PRINT SGN(A), SGN(B), ABS(A), RND(10)
430 PRINT "-1 1 1"
500 PRINT "*** Arduino features ***"
510 PRINT "Blink"
520 PINM LED, 1
530 FOR I=1 TO 4: DWRITE LED, 1: DELAY 200: DWRITE LED, 0: NEXT
540 PRINT "Time"
550 PRINT MILLIS(1): DELAY 200: PRINT MILLIS(1)
560 PRINT "EEPROM"
570 PRINT "Size:", @E
580 FOR I=1 TO 4: PRINT I, @E(I): NEXT
590 @E(1)=10: PRINT "10=",@E(1)
600 PRINT "*** Dartmouth features ***"
610 DEF FNF(X)=X*X 
620 X=5: PRINT "X=", X 
630 PRINT "FNF(7)=", FNF(7)
640 PRINT "X=",X 
650 FOR I=1 TO 3
660 ON I GOSUB 700, 710, 720
670 NEXT
680 GOTO 800
700 PRINT "One": RETURN
710 PRINT "Two": RETURN
720 PRINT "Three": RETURN
730 DATA "Hello", "World", 31414
800 READ A$, B$: PRINT A$,B$
810 READ A: PRINT "PI is",A
820 PRINT "DATA pointer is", DATA
900 PRINT "*** Strings ***"
910 A$="Hello World"
920 PRINT A$, ":", LEN(A$)
930 FOR I=1 TO LEN(A$)
940 PRINT TAB(I-1);A$(I)
950 NEXT
960 FOR I=1 TO LEN(A$)
970 PRINT A$(1,I)
980 NEXT
990 B$="Arduino says "
1000 B$=B$+A$: PRINT B$
1010 B$=STR$(A): PRINT "PI is", B$
1020 B=VAL(B$): PRINT "PI is", B 
1030 PRINT "String length pointer is", @V
1040 FOR I=1 TO 5
1050 PRINT TAB(I-1);MID$(A$,I,LEN(A$)-2*I+1)
1060 NEXT
1070 FOR I=1 TO 5: PRINT LEFT$(A$,I): NEXT
1080 FOR I=1 TO 5: PRINT TAB(5-I);RIGHT$(A$,I): NEXT


















