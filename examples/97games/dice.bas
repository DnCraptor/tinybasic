10 PRINT "DICE from 101 BASIC games"
11 PRINT "Originally written by Danny Freidus"
12 PRINT "Ported to Stefan's BASIC in 2021"
15 PRINT
20 DIM F(12)
30 PRINT "THIS PROGRAM SIMULATES THE ROLLING OF A"
40 PRINT "PAIR OF DICE."
50 PRINT "YOU ENTER THE NUMBER OF TIMES YOU WANT THE COMPUTER TO"
60 PRINT "'ROLL' THE DICE.  WATCH OUT, VERY LARGE NUMBERS TAKE"
70 PRINT "A LONG TIME.  IN PARTICULAR, NUMBERS OVER 5000."
80 FOR Q=1 TO 12
90 F(Q)=0
100 NEXT Q
110 PRINT:PRINT "HOW MANY ROLLS";
120 INPUT X
130 FOR S=1 TO X
140 A=INT(RND(6)+1)
150 B=INT(RND(6)+1)
160 R=A+B
170 F(R)=F(R)+1
180 NEXT S
185 PRINT
190 PRINT "TOTAL SPOTS","NUMBER OF TIMES"
200 FOR V=2 TO 12
210 PRINT V,F(V)
220 NEXT V
221 PRINT
222 PRINT:PRINT "TRY AGAIN";
223 INPUT Z$
224 IF Z$="YES" OR Z$="yes" THEN 80
240 END
