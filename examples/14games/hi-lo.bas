10 REM "HI LO from 101 BASIC games"
20 REM "Ported to Stefans BASIC in 2021"
30 REM "Randomize, @R is the seed and millis the expired time."
40 @R=MILLIS(1)
100 PRINT "THIS IS THE GAME OF HI LO.":PRINT
110 PRINT "YOU WILL HAVE 6 TRIES TO GUESS THE AMOUNT OF MONEY IN THE"
120 PRINT "HI LO JACKPOT, WHICH IS BETWEEN 1 AND 100 DOLLARS.  IF YOU"
130 PRINT "GUESS THE AMOUNT, YOU WIN ALL THE MONEY IN THE JACKPOT!"
140 PRINT "THEN YOU GET ANOTHER CHANCE TO WIN MORE MONEY.  HOWEVER,"
150 PRINT "IF YOU DO NOT GUESS THE AMOUNT, THE GAME ENDS.": PRINT
160 R=0
170 B=0
180 Y=INT(RND(100))
200 PRINT
210 INPUT "YOUR GUESS? ",A
220 B=B+1
230 IF A=Y THEN 400
240 IF A>Y THEN 270
250 PRINT "YOUR GUESS IS TOO LOW.": GOTO 280
270 PRINT "YOUR GUESS IS TOO HIGH."
280 PRINT
290 IF B<6 THEN 200
300 PRINT "YOU BLEW IT...TOO BAD...THE NUMBER WAS", Y
310 R=0
320 GOTO 450
400 PRINT "GOT IT!!!!!!!!!!   YOU WIN", Y, "DOLLARS."
410 R=R+Y
420 PRINT "YOUR TOTAL WINNINGS ARE NOW", R, "DOLLARS."
450 PRINT
460 INPUT "PLAY AGAIN (YES OR NO)? ", A$:
470 IF A$="YES" OR A$="yes" THEN 170
480 PRINT "SO LONG.  HOPE YOU ENJOYED YOURSELF!!!"
490 END
