10 REM "CHEMIST from 101 BASIC games"
20 REM "Ported to Stefan's BASIC in 2021"
30 REM
100 PRINT "THE FICTITIOUS CHECMICAL KRYPTOCYANIC ACID CAN ONLY BE"
110 PRINT "DILUTED BY THE RATIO OF 7 PARTS WATER TO 3 PARTS ACID."
120 PRINT "IF ANY OTHER RATIO IS ATTEMPTED, THE ACID BECOMES UNSTABLE"
130 PRINT "AND SOON EXPLODES.  GIVEN THE AMOUNT OF ACID, YOU MUST"
140 PRINT "DECIDE HOW MUCH WATER TO ADD FOR DILUTION.  IF YOU MISS"
150 PRINT "YOU FACE THE CONSEQUENCES." : PRINT
160 @R=MILLIS(1)
170 C=0: S=0 
200 REM "Main loop"
210 A=INT(RND(50))
220 W=7*A/3
230 PRINT A,"LITERS OF KRYPTOCYANIC ACID.  HOW MUCH WATER";
240 Q=MILLIS(100)
250 INPUT R
260 Q=MILLIS(100)-Q
270 D=ABS(W-R)
280 IF D>W/20 THEN 400
290 S=S+Q: C=C+1
300 PRINT " GOOD JOB! YOU MAY BREATHE NOW, BUT DON'T INHALE THE FUMES!"
310 PRINT "AVERAGE REPLY TIME SO FAR", INT(S/C/10), "SECONDS"
320 GOTO 200
400 PRINT " SIZZLE!  YOU HAVE JUST BEEN DESALINATED INTO A BLOB"
410 PRINT " OF QUIVERING PROTOPLASM!"
420 T=T+1
430 IF T=9 THEN 500
440 PRINT " HOWEVER, YOU MAY TRY AGAIN WITH ANOTHER LIFE."
450 GOTO 200
500 PRINT " YOUR 9 LIVES ARE USED, BUT YOU WILL BE LONG REMEMBERED FOR"
510 PRINT " YOUR CONTRIBUTIONS TO THE FIELD OF COMIC BOOK CHEMISTRY."
900 END
