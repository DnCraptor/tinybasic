10 REM "Craps from 101 BASIC games"
20 REM "Originally from DEC, ported to Stefan's BASIC in 2021"
30 @R=MILLIS(1)
100 PRINT "CRAPS FROM 101 BASIC GAMES": PRINT
110 R=0
120 PRINT"2,3,12 ARE LOSERS; 4,5,6,8,9,10 ARE POINTS;"
130 PRINT"7,11 ARE NATURAL WINNERS."
140 PRINT
150 T=1
200 PRINT"INPUT THE AMOUNT OF YOUR WAGER.";
210 INPUT F
220 PRINT "I WILL NOW THROW THE DICE"
230 E=INT(RND(7))
240 S=INT(RND(7))
250 X=E+S
300 REM "Evaluate result"
310 IF X=7 OR X=11 THEN 400
320 IF X=1 OR X=0 THEN 230
330 IF X=2 OR X=3 OR X=12 THEN 700
340 IF X>=4 AND X<=10 THEN 800
400 PRINT X, "- NATURAL....A WINNER!!!!"
410 PRINT X, "PAYS EVEN MONEY, YOU WIN",F, "DOLLARS"
420 GOTO 730
500 PRINT X"- SNAKE EYES....YOU LOSE."
510 PRINT "YOU LOSE",F ,"DOLLARS."
600 F=-F
610 GOTO 730
700 PRINT X, " - CRAPS...YOU LOSE."
710 PRINT "YOU LOSE", F, "DOLLARS."
720 F=-F
730 R=R+F
740 GOTO 1100
800 PRINT X, "IS THE POINT. I WILL ROLL AGAIN"
810 H=INT(RND(7))
820 Q=INT(RND(7))
830 O=H+Q
840 IF O=1 OR Q=0 THEN 810
850 IF O=7 THEN 900
870 IF O=X THEN 1000
880 PRINT O, " - NO POINT. I WILL ROLL AGAIN"
890 GOTO 810
900 PRINT O, "- CRAPS. YOU LOSE."
910 PRINT "YOU LOSE $", F
920 F=-F
930 GOTO 730
940 GOTO 1100
1000 PRINT X, "- A WINNER.........CONGRATS!!!!!!!!"
1010 PRINT X, "AT 2 TO 1 ODDS PAYS YOU...LET ME SEE...", 2*F, "DOLLARS"
1020 F=2*F
1030 GOTO 730
1100 PRINT "** IF YOU WANT TO PLAY AGAIN PRINT 5 IF NOT PRINT 2";
1110 INPUT M
1120 IF R<0 THEN PRINT "YOU ARE NOW UNDER $",-R
1130 IF R>0 THEN PRINT "YOU ARE NOW AHEAD $",R
1140 IF R=0 THEN PRINT "YOU ARE NOW EVEN AT 0"
1200 IF M=5 THEN 200
1210 IF R<0 THEN 1240
1220 IF R>0 THEN 1260
1230 IF R=0 THEN 1280
1240 PRINT "TOO BAD, YOU ARE IN THE HOLE. COME AGAIN."
1250 GOTO 1300
1260 PRINT "CONGRATULATIONS---YOU CAME OUT A WINNER. COME AGAIN!"
1270 GOTO 1300
1280 PRINT "CONGRATULATIONS---YOU CAME OUT EVEN, NOT BAD FOR AN AMATEUR"
1300 END
