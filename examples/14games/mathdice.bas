10 REM "MATH DICE from 101 BASIC games"
20 REM "Ported to Stefan's BASIC in 2021"
30 REM "E counts the errors , R the response time"
40 E=0
50 R=0
60 C=0
100 PRINT "THIS PROGRAM GENERATES SUCCESIVE PICTURES OF TWO DICE."
110 PRINT "WHEN TWO DICE AND AN EQUAL SIGN FOLLOWED BY A QUESTION"
120 PRINT "MARK HAVE BEEN PRINTED, TYPE YOUR ANSWER AND THE RETURN KEY."
130 PRINT "TO CONCLUDE THE LESSON, TYPE # AS YOUR ANSWER."
140 PRINT
160 @R=MILLIS(1)
200 REM "loop() the main loop"
210 N=N+1
220 D=INT(6*RND(1)+1)
230 PRINT" ----- "
240 REM "First row"
250 IF D=1 THEN PRINT "I     I" : GOTO 300
260 IF D=2 OR D=3 THEN PRINT "I *   I": GOTO 300
270 PRINT "I * * I"
300 REM "Second row"
310 IF D=2 OR D=4 THEN PRINT "I     I": GOTO 400
320 IF D=6 THEN PRINT "I * * I": GOTO 400
330 PRINT "I  *  I"
400 REM "Third row"
410 IF D=1 THEN PRINT "I     I": GOTO 500
420 IF D=2 OR D=3 THEN PRINT "I   * I": GOTO 500
430 PRINT "I * * I"
500 PRINT " ----- "
510 PRINT
520 IF N=2 THEN 600
530 PRINT "   +"
540 PRINT
550 A=D
560 GOTO 200
600 REM "The math"
610 T=D+A
620 PRINT "      =";
630 S0=MILLIS(100)
640 INPUT T1
650 IF T1=0 THEN 900
660 IF T1=T THEN 800
670 PRINT "NO, COUNT THE SPOTS AND GIVE ANOTHER ANSWER."
680 PRINT "      =";
700 INPUT T2
710 IF T2=0 THEN 900
720 IF T2=T THEN 800
730 PRINT "NO, THE ANSWER IS",T
740 E=E+1
750 GOTO 820
800 PRINT "RIGHT!"
810 S1=MILLIS(100)
820 PRINT "REPLY TIME: "; INT((S1-S0)/10);"."; INT(S1-S0)%10;" SECONDS"
830 R=R+(S1-S0)
840 PRINT
850 PRINT "THE DICE ROLL AGAIN..."
860 PRINT
870 N=0: C=C+1
880 GOTO 200
900 REM "Report response times"
910 IF C=0 THEN END
920 PRINT "TRIES: "; C
930 PRINT "AVERAGE RESPONSE TIME: "; INT(R/C/10); "."; INT(R/C)%10, "SECONDS"
940 PRINT "ERRORS: "; E 
950 END
