10 REM "HURKLE from 101 BASIC games"
20 REM "Ported to Stefan's BASIC in 2021"
30 REM "G is the grid size, N the number of attempts"
40 @R=MILLIS(1)
50 N=5
60 G=10
100 PRINT "A HURKLE IS HIDING ON A",G,"BY",G,"GRID. HOMEBASE"
110 PRINT "ON THE GRID IS POINT 0,0 IN THE SOUTHWEST CORNER,"
120 PRINT "AND ANY POINT ON THE GRID IS DESIGNATED BY A"
130 PRINT "PAIR OF WHOLE NUMBERS SEPERATED BY A COMMA. THE FIRST"
140 PRINT "NUMBER IS THE HORIZONTAL POSITION AND THE SECOND NUMBER"
150 PRINT "IS THE VERTICAL POSITION. YOU MUST TRY TO"
160 PRINT "GUESS THE HURKLE'S GRIDPOINT. YOU GET",N,"TRIES."
170 PRINT "AFTER EACH TRY, I WILL TELL YOU THE APPROXIMATE"
180 PRINT "DIRECTION TO GO TO LOOK FOR THE HURKLE."
190 PRINT "TYPE # TO END PROGRAM": PRINT
200 REM "The hukles position"
210 A=INT(RND(G))
220 B=INT(RND(G))
300 REM "The guess loop"
310 FOR K=1 TO N
320 PRINT "GUESS #";K;
330 REM "Safe input of two numbers"
340 INPUT A$
350 IF A$="#" THEN END
360 @S=0: X=VAL(A$)
370 IF @S THEN PRINT "NUMBER ERROR": GOTO 320
380 I=INSTR(A$, ",")
390 IF I=0 THEN PRINT "NUMBER ERROR": GOTO 320
400 @S=0: Y=VAL(A$(I+1))
410 IF @S THEN PRINT "NUMBER ERROR": GOTO 320
420 IF X=A AND Y=B THEN 600
430 GOSUB 800
440 PRINT
450 NEXT K
510 PRINT
520 PRINT "SORRY, THAT'S",N,"GUESSES."
530 PRINT "THE HURKLE IS AT ",A,",",B
540 PRINT
550 PRINT "LET'S PLAY AGAIN, HURKLE IS HIDING."
560 PRINT
570 GOTO 200
600 REM "Found him"
610 PRINT
620 PRINT "YOU FOUND HIM IN",K,"GUESSES!"
640 GOTO 540
800 REM "Output Answer"
810 PRINT "GO ";
820 IF Y<B THEN PRINT "NORTH";
830 IF Y>B THEN PRINT "SOUTH";
840 IF X<A THEN PRINT "EAST"; 
850 IF X>A THEN PRINT "WEST";
860 PRINT
870 RETURN
900 END
