10 REM "CHANGE from 101 BASIC games"
20 REM "Ported to Stefan's BASIC in 2021"
30 REM
100 PRINT "I, YOUR FRIENDLY MICROCOMPUTER, WILL DETERMINE"
110 PRINT "THE CORRECT CHANGE FOR ITEMS COSTING UP TO $100."
120 PRINT:PRINT
200 FOR T
210 INPUT "COST OF ITEM? ", A
220 INPUT "AMOUNT OF PAYMENT? ", P
230 C=P-A:M=C:
240 IF C=0 THEN PRINT "CORRECT AMOUNT, THANK YOU.": CONT
250 IF C<0 THEN PRINT "SORRY, YOU HAVE SHORT-CHANGED ME $",A-P: GOTO 210
300 PRINT "YOUR CHANGE, $",C
310 D=INT(C/10)
320 IF D<>0 THEN PRINT D,"TEN DOLLAR BILL(S)"
330 C=M-(D*10)
340 E=INT(C/5)
350 IF E<>0 THEN PRINT E,"FIVE DOLLARS BILL(S)"
360 C=M-(D*10+E*5)
370 F=INT(C)
380 IF F<>0 THEN PRINT F,"ONE DOLLAR BILL(S)"
390 C=M-(D*10+E*5+F)
400 C=C*100
410 N=C
420 G=INT(C/50)
430 IF G<>0 THEN PRINT G,"ONE HALF DOLLAR(S)"
440 C=N-(G*50)
450 H=INT(C/25)
460 IF H<>0 THEN PRINT H,"QUARTER(S)"
470 C=N-(G*50+H*25)
480 I=INT(C/10)
490 IF I<>0 THEN PRINT I,"DIME(S)"
500 C=N-(G*50+H*25+I*10)
510 J=INT(C/5)
520 IF J<>0 THEN PRINT J,"NICKEL(S)"
530 C=N-(G*50+H*25+I*10+J*5)
540 K=INT(C+1/2)
550 IF K<>0 THEN PRINT K,"PENNY(S)"
560 PRINT "THANK YOU, COME AGAIN."
570 PRINT
600 NEXT T
900 END
