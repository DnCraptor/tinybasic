10 REM "MUGWUMP from 101 BASIC games"
20 REM "Ported to Stefan's BASIC in 2021"
30 REM
40 DIM P(4*2)
50 @R=MILLIS(1)
60 DEF FND(I)=SQR(POW(P(I)-M, 2)+POW(P(I+4)-N,2))
100 PRINT "THE OBJECT OF THIS GAME IS TO FIND FOUR MUGWUMPS"
110 PRINT "HIDDEN ON A 10 BY 10 GRID.  HOMEBASE IS POSITION 0,0."
120 PRINT "ANY GUESS YOU MAKE MUST BE TWO NUMBERS WITH EACH"
130 PRINT "NUMBER BETWEEN 0 AND 9, INCLUSIVE.  FIRST NUMBER"
140 PRINT "IS DISTANCE TO RIGHT OF HOMEBASE AND SECOND NUMBER"
150 PRINT "IS DISTANCE ABOVE HOMEBASE.": PRINT
160 PRINT "YOU GET 10 TRIES.  AFTER EACH TRY, I WILL TELL"
170 PRINT "YOU HOW FAR YOU ARE FROM EACH MUGWUMP."
180 PRINT
190 GOSUB 1000
200 FOR T=1 TO 10
210 PRINT
220 PRINT "TURN NO.",T,"-- WHAT IS YOUR GUESS"
230 INPUT "X: ",M
240 INPUT "Y: ", N
300 FOR I=1 TO 4
320 IF P(I)=-1 THEN CONT
330 IF P(I)=M AND P(I+4)=N THEN P(I)=-1: PRINT "YOU HAVE FOUND MUGWUMP",I
340 ELSE D=INT(FND(I)*10)/10: PRINT "YOU ARE",D ,"UNITS FROM MUGWUMP",I
400 NEXT I
410 FOR J=1 TO 4
420 IF P(J)<>-1 THEN BREAK
430 NEXT J
440 IF J=5 THEN PRINT "YOU GOT THEM ALL IN",T,"TURNS!": BREAK
470 NEXT T
480 IF T<11 THEN 600 
490 PRINT "SORRY, THAT'S 10 TRIES.  HERE IS WHERE THEY'RE HIDING:"
500 FOR I=1 TO 4
510 IF P(I)=-1 THEN CONT
520 PRINT "MUGWUMP",I,"IS AT (",P(I),",",P(I+4),")"
530 NEXT I
600 PRINT "THAT WAS FUN! LET'S PLAY AGAIN......."
610 PRINT "FOUR MORE MUGWUMPS ARE NOW IN HIDING."
620 GOTO 190
630 END
1000 FOR J=1 TO 2
1010 FOR I=1 TO 4
1020 P(I+(J-1)*4)=INT(RND(10))
1030 NEXT I
1040 NEXT J
1050 RETURN
1099 END
