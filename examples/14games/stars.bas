10 REM "STARS from 101 BASIC games"
20 REM "Originally from Bob Albrecht in 1975"
30 REM "Ported to Stefan's BASIC in 2021"
40 REM
50 @R=MILLIS(1)
60 A=100:M=7
170 INPUT "STARS: DO YOU WANT INSTRUCTIONS? ";A$
190 IF A$(1)="N" THEN 280
200 REM "INSTRUCTIONS"
210 PRINT "I AM THINKING OF A WHOLE NUMBER FROM 1 TO",A
220 PRINT "TRY TO GUESS MY NUMBER.  AFTER YOU GUESS, I"
230 PRINT "WILL TYPE ONE OR MORE STARS (*).  THE MORE"
240 PRINT "STARS I TYPE, THE CLOSER YOU ARE TO MY NUMBER."
250 PRINT "ONE STAR (*) MEANS FAR AWAY, SEVEN STARS (*******)"
260 PRINT "MEANS REALLY CLOSE!  YOU GET",M,"GUESSES."
270 REM "COMPUTER THINKS OF A NUMBER"
280 PRINT
290 PRINT
300 X=INT(RND(A))+1
310 PRINT "OK, I AM THINKING OF A NUMBER, START GUESSING."
320 REM "GUESSING BEGINS, HUMAN GETS M GUESSES"
330 FOR K=1 TO M
340 PRINT
350 PRINT "YOUR GUESS";
360 INPUT G
370 IF G=X THEN 600
380 D=ABS(G-X)
390 IF D>=64 THEN 510
400 IF D>=32 THEN 500
410 IF D>=16 THEN 490
420 IF D>=8 THEN 480
430 IF D>=4 THEN 470
440 IF D>=2 THEN 460
450 PRINT "*";
460 PRINT "*";
470 PRINT "*";
480 PRINT "*";
490 PRINT "*";
500 PRINT "*";
510 PRINT "*";
520 PRINT
530 NEXT K
540 REM "DID NOT GUESS IN M GUESSES"
550 PRINT 
560 PRINT "SORRY, THAT'S",M,"GUESSES. THE NUMBER WAS",X
580 GOTO 650
590 REM "WE HAVE A WINNER"
600 PRINT:FOR N=1 TO 40
610 PRINT "*";
620 NEXT N
630 PRINT:PRINT
640 PRINT "YOU GOT IT IN",K,"GUESSES!!!  LET'S PLAY AGAIN..."
650 GOTO 280
660 END
