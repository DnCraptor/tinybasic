10 REM "ROCK, SCISSORS, PAPER from 101 BASIC games"
20 REM "Ported to Stefan's BASIC in 2021"
30 REM "C and H count results"
40 @R=MILLIS(1)
50 C=0: H=0
100 PRINT "A GAME OF ROCK, SCISSORS, PAPER"
110 INPUT "HOW MANY GAMES? ";Q
120 IF Q<0 THEN 110
200 FOR G=1 TO Q
210 PRINT "GAME NUMBER ";G
220 X=INT(RND(3)+1)
230 PRINT "3=ROCK...2=SCISSORS...1=PAPER"
240 INPUT "1...2...3...WHAT'S YOUR CHOICE? ";K
250 K=INT(K)
260 IF (K-1)*(K-2)*(K-3)<>0 THEN PRINT "INVALID.": GOTO 240
270 PRINT "THIS IS MY CHOICE..."
280 IF X=1 THEN PRINT "...PAPER"
290 IF X=2 THEN PRINT "...SCISSORS"
300 IF X=3 THEN PRINT "...ROCK"
400 REM "Evaluate result"
410 IF X=K THEN PRINT "TIE GAME.  NO WINNER.": CONT
420 IF X>K THEN 470
430 IF X=1 THEN 450
440 PRINT "YOU WIN!!!":H=H+1: CONT
450 IF K<>3 THEN 440
460 PRINT "WOW!  I WIN!!!":C=C+1: CONT
470 IF K<>1 OR X<>3 THEN 460
480 GOTO 440
490 NEXT G
500 PRINT: PRINT "HERE IS THE FINAL GAME SCORE:"
510 PRINT "I HAVE WON",C,"GAME(S)."
520 PRINT "YOU HAVE WON",H,"GAME(S)."
530 PRINT "AND",Q-(C+H),"GAME(S) ENDED IN A TIE."
540 PRINT: PRINT "THANKS FOR PLAYING!!"
800 END
