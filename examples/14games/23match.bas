10 REM "23 Matches from 101 BASIC games"
20 REM "Originally written by Bob Albrecht in 1975"
30 REM "Ported to Stefan's BASIC in 2021"
40 REM "Randomization"
50 @R=MILLIS(1)
100 PRINT "THIS IS A GAME CALLED '23 MATCHES'.": PRINT
110 PRINT "WHEN IT IS YOUR TURN, YOU MAY TAKE ONE, TWO, OR THREE"
120 PRINT "MATCHES. THE OBJECT OF THE GAME IS NOT TO HAVE TO TAKE"
130 PRINT "THE LAST MATCH.": PRINT
140 PRINT "LET'S FLIP A COIN TO SEE WHO GOES FIRST."
150 PRINT "IF IT COMES UP HEADS, I WILL WIN THE TOSS.": PRINT
160 N=23
170 Q=INT(RND(2))
180 IF Q=1 THEN 210
190 PRINT "TAILS! YOU GO FIRST. ": PRINT
200 GOTO 290
210 PRINT "HEADS! I GO FIRST!": PRINT
250 PRINT "I TAKE 2 MATCHES"
260 N=N-2
270 PRINT "THE NUMBER OF MATCHES IS NOW", N
280 PRINT
290 PRINT "YOU MAY TAKE 1, 2 OR 3 MATCHES."
300 INPUT "HOW MANY DO YOU WISH TO REMOVE? ", K
320 IF K>3 OR K<=0 THEN 430
340 N=N-K
350 PRINT "THERE ARE NOW", N, " MATCHES REMAINING."
355 IF N=4 THEN Z=3: GOTO 390
360 IF N=3 THEN Z=2: GOTO 390
365 IF N=2 THEN Z=1: GOTO 390
370 IF N<=1 THEN 530
375 Z=4-K
390 PRINT "MY TURN ! I REMOVE ", Z, " MATCHES"
400 N=N-Z
410 IF N<=1 THEN 470
420 GOTO 270
430 PRINT "YOU CAN ONLY REMOVE 1, 2, OR 3 MATCHES."
440 GOTO 300
470 PRINT
480 PRINT "YOU TOOK THE LAST MATCH! I WIN!"
490 PRINT "HA! HA! I BEAT YOU!!!"
520 GOTO 570
530 PRINT "YOU WON!"
540 PRINT "YOU'RE PRETTY SMART!"
570 END
