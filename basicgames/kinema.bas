10 PRINT "KINEMA from 101 BASIC games"
20 PRINT "Ported to Stefan's BASIC in 2021""
30 PRINT
40 @R=MILLIS(1)
100 PRINT
106 Q=0
110 V=5+INT(RND(35))
111 PRINT "A BALL IS THROWN UPWARDS AT",V,"METERS PER SECOND."
112 PRINT
115 A=0.05*V*V
116 PRINT "HOW HIGH WILL IT GO (IN METERS)";
117 GOSUB 500
120 A=V/5
122 PRINT "HOW LONG UNTIL IT RETURNS (IN SECONDS)";
124 GOSUB 500
130 T=1+INT(V*RND(2))/10
132 A=V-10*T
134 PRINT "WHAT WILL ITS VELOCITY BE AFTER",T,"SECONDS";
136 GOSUB 500
140 PRINT
150 PRINT Q,"RIGHT OUT OF 3.";
160 IF Q<2 THEN 100
170 PRINT "  NOT BAD."
180 GOTO 100
500 INPUT G
502 IF ABS((G-A)/A)<0.15 THEN 510
504 PRINT "NOT EVEN CLOSE...."
506 GOTO 512
510 PRINT "CLOSE ENOUGH."
511 Q=Q+1
512 PRINT "CORRECT ANSWER IS",A
520 PRINT
530 RETURN
999 END
